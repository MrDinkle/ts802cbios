;	***********************************************
;       TS-802H BOOT SECTOR FOR TANDON 5 1/4 WINCHESTER
;                    AND 5 1/4 FLOPPY DISK
;	***********************************************

;	Edited by TUYEN TRAN date 5/20/81
;
;	Modified by SUNG WOO LEE date 9/18/81
;             to be adopted for TS-802H
;
;	    * Executes at 4000H
;	    * If input A = 00h then read in CP/M from hard disk.
;		         = 01h then read in CP/M from floppy disk.
;	    * Turns off PROM
;	    * Read in CP/M and BIOS to CCP address
;	    * Jump to BIOS=CCP+1600h

	CSEG
	.PHASE  4000H
	.Z80

;	FOLLOWING CCP ADDRESS HAS TO BE CONSULTED 
;	WITH 802HBIOS.MAC FOR MOVCPM GENERATION.

CCP	EQU	0D800H		; CP/M ENTRY POINT
BIOS	EQU	CCP+1600H	; BIOS ENTRY POINT	

NSECTS	EQU	(0FFFFH-CCP)/256+1	; # OF 256 BYTES SECTOR TO LOAD
					; BEGINING OF CCP TO END OF MOMORY
PRMOFF	EQU	04H		; PORT TO TURN PRON OFF
LED	EQU	80H
	
;   	DRIVE STATUS

WFAULT	EQU	020H		; WRITE FAULT ON DRIVE

;   	CONTROLLER COMMAND

WREAD	EQU	020H		; READ SECTOR

;   	CONTROLLER STATUS

WBUSY	EQU	080H		; CONTROLLER BUSY
WDRQ	EQU	008H		; DATA REQUEST
WERROR	EQU	001H		; ERROR STATUS PRESENT

;   	DRIVE/CONTROLLER I/O

CBASE	EQU	48H		; BASE ADDRESS OF THE CONTROLLER

WDATA	EQU	CBASE		; DATA PORT (I/O)
WPCYL	EQU	CBASE+1		; WRITE PRECOMP CYLINDER NUMBER (O)
WERRS	EQU	CBASE+1		; ERROR DEFINITION (I)
WCOUNT	EQU	CBASE+2		; SECTOR COUNT (I/O)
WSECT	EQU	CBASE+3		; SECTOR NUMBER (I/O)
WLOCYL	EQU	CBASE+4		; CYLINDER NUMBER, LOW ORDER BITS(I-O)
WHICYL	EQU	CBASE+5		; CYLINDER NUMBER, HIGH ORDER BITS (I/O)
WSDH	EQU	CBASE+6		; SECTOR SIZE, DRIVE SELECT, HEAD SELECT (I/O)
WCMD	EQU	CBASE+7		; COMMAND PORT (O)
WSTAT	EQU	CBASE+7		; STATUS PORT (I)

;	FLOPPY DISK CONTROLLER EQUATES

DSELOP	EQU	18H		; DRIVE SELECT AND CONFIG. PORT ADDRESS
FDCCMD	EQU	14H		; COMMAND AND STATUS PORT.
FDCTRK	EQU	15H		; TRACK REGISTER PORT
FDCSEC	EQU	16H		; SECTOR REGISTER PORT
FDCDAT	EQU	17H		; DATA REGISTER PORT

;	DMA PORT ADDRESS

DMA	EQU	10H		; DMA DEVICE PORT ADDRESS

;	FDC COMMAND BYTES

RESCMD	EQU	00000110B	; HOME HEAD COMMAND.
SEECMD	EQU	00010010B	; SEEK TRACK COMMAND WITH NO VERIFY
RDSCMD	EQU	10000000B	; READ SECTOR
WTSCMD	EQU	10100000B	; WRITE SECTOR

FSPT	EQU	18		; NUMBER OF SECTOR PER TRACK



	OUT	(PRMOFF),A	; TURN PROM OFF
	OUT	(LED),A		; show we got here
	LD	SP,STACKP	; INITIALIZE STACK POINTER.
	OR	A		; CHECK 'BOOT' FROM WHERE
	JP	NZ,BFLOPPY	; IF FROM FLOPPY THEN SKIP 

;	ELSE LOAD CP/M FROM WINCHESTER.

	INC	A		; PRESET RECNO = 1
	LD	(RECNO),A
	LD	HL,CCP

LOOP:
	LD	A,(H$TRK)
	AND	3
	OUT	(WSDH),A
	XOR	A
	OUT	(WHICYL),A
	OUT	(WLOCYL),A
	LD	A,(H$SEC)
	OUT	(WSECT),A
	LD	A,1
	OUT	(WCOUNT),A

	LD	A,WREAD
	OUT	(WCMD),A

GETIO:	IN	A,(WSTAT)
	AND	WDRQ
	JR	Z,GETIO

;	SET B = TRANSFER COUNT; C = INPUT DATA PORT ADDRESS

	LD	BC,WDATA

;	TRANSFER DATA FROM CONTROLLER TO MEMORY

	INIR	

WAIT:	IN	A,(WSTAT)
	AND	WBUSY
	JR	NZ,WAIT

	DEC	H		; SET HL = LAST DMA ADDRESS
	IN	A,(WSTAT)	; CHECK FOR ERROR
	AND	WFAULT+WERROR
	JR	NZ,LOOP		; IF ERROR THEN RETRY TRANS. DATA BLOCK
	INC	H		; ELSE HL = NEXT DMA ADDRESS.

	EX	DE,HL

	LD	HL,RECNO
	LD	A,(HL)
	SUB	NSECTS

	JP	Z,BIOS

	INC	(HL)
	DEC	HL		; ADDRESS H$SEC
	INC	(HL)
	LD	A,(HL)
	CP	32
	EX	DE,HL
	JR	NZ,LOOP
	EX	DE,HL
	XOR	A
	LD	(HL),A
	DEC	HL
	DEC	HL		; ADDRESS H$TRK
	INC	(HL)		; SET TRACK NO. TO NEXT

	EX	DE,HL
	JR	LOOP

BFLOPPY:

;	BOOT SYSTEM IS FAILED ON WINCHESTER HARD DISK
;	NOW TRY TO BOOT FROM FLOPPY.

	LD	HL,H$TRK	; SYSTEM START AT TRACK #0
	XOR	A
	LD	(HL),A
	INC	HL
	INC	HL
	LD	(HL),2		; SECTOR #2
	INC	A
	INC	HL
	LD	(HL),A		; PRESET RECNO = 1

BFLOP0:

;	INITIALIZE DMA DEVICE

	LD	B,6
	LD	A,11000011B	; RESET DMA

BFLOP1:
	OUT	(DMA),A
	DJNZ	BFLOP1

BFLOP2:
	LD	HL,DMAINI	; ADDRESS DMA INITIALIZE TABLE
	LD	BC,TABSIZ*256+DMA

	OTIR	

;	INITIALIZE FDC REGISTERS

	LD	A,(H$TRK)
	OUT	(FDCTRK),A	; SET FDC TRACK REGISTER
	LD	A,(H$SEC)
	LD	B,00010010B	; SET B = DRIVE #0 AND SEL.SIDE #0
	CP	FSPT+1		; NEED TO SELECT SIDE #1 ?
	JR	C,BFLOP4	; IF NO THEN SKIP
	SUB	FSPT		; ELSE CONVERT SECTOR FOR SIDE #1 READ
	LD	B,00010110B	; SET B = DRIVE #0 AND SEL.SIDE #1

BFLOP4:
	OUT	(FDCSEC),A	; SET FDC SECTOR REGISTER
	LD	A,B		; SET DISK CONFIG.

	OUT	(DSELOP),A	; SELECT DRIVE, SIDE, DENSITY, TURN MOTOR ON
	LD	A,RDSCMD	; READ SECTOR INTO MEMORY
	CALL	BFLOP6
	JR	NZ,BFLOP0	; IF ERROR THEN RETRY AGAIN.

	LD	HL,H$SEC	; SET SECTOR ADDRESS TO NEXT.
	INC	(HL)		; IF END OF SECTOR ADDRESS ON SIDE #1 OF 
	LD	A,(HL)		; CURRENT TRACK THEN
	CP	(FSPT*2)+1	; PRESET SECTOR ADDRESS TO 1; TRACK = TRACK+1
	JR	C,BFLOP9	; ELSE SECTOR = SECTOR+1
	LD	(HL),1
	DEC	HL
	DEC	HL
	INC	(HL)
	LD	A,(HL)
	OUT	(FDCDAT),A

;	SEEK TO NEW TRACK

BFLO10:
	LD	A,SEECMD
	CALL	BFLOP6		; PERFORM SEEK OPERATION
	JR	Z,BFLOP9	; IF NO ERROR THEN SKIP
	LD	A,RESCMD	; ELSE RESET HEAD TO HOME IF ERROR
	CALL	BFLOP6
	JR	BFLO10		; RETRY SEEK OPERATION

BFLOP9:
	LD	HL,RECNO	; CHECK READ COUNT
	LD	A,(HL)
	CP	NSECTS		; IF FINISH
	LD	A,1		; SET A = 1 AND GO TO BIOS COLD BOOT ENTRY
	JP	Z,BIOS
	INC	(HL)		; ELSE READ COUNT = COUNT + 1
	LD	HL,TRANADD+1	; SET DMA ADDRESS FOR NEXT READ
	INC	(HL)
	JR	BFLOP2		; READ NEXT SECTOR IN

BFLOP6:
	OUT	(FDCCMD),A	; NOW READ SECTOR INTO MEMORY

	LD	A,15		; WE MUST DELAY ABOUT 60 US BEFOR READING

BFLOP7:
	DEC	A		; FDC STATUS
	JR	NZ,BFLOP7

BFLOP8:
	IN	A,(FDCCMD)	; READ FDC STATUS
	LD	B,A
	AND	10000000B	; CHECK FOR DRIVE NOT READY?
	RET	NZ
	LD	A,B
	AND	00000001B	; CHECK OPERATION COMPLETED
	JR	NZ,BFLOP8	; WAIT UNTIL NOT BUSY
	LD	A,B
	AND	00011100B	; ANY ERROR: RNF,CRC,LOST DATA?
	RET
	

DMAINI:
	DEFB	11000011B	; RESET DMA CHANNEL
	DEFB	01101101B	; PORT 'A' ADDRESS FOLLOWS
	DEFB	FDCDAT		; FDC DATA PORT ADDRESS
	DEFW	0FFH		; READ TRANSFER COUNT
	DEFB	00111100B	; PORT 'A' IS FIXED
	DEFB	00010000B	; PORT 'B' MEMORY, INC.
	DEFB	11001101B	; PORT 'B' ADDRESS FOLLOWS

TRANADD:
	DEFW	CCP		; CP/M LOAD ADDRESS
	DEFB	10001010B	; READY ACTIVE HIGH, END OF BLOCK
	DEFB	11001111B	; LOAD REGISTERS
	DEFB	00000101B	; A -> B TRANSFER MODE
	DEFB	11001111B	; LOAD REGISTERS
	DEFB	10000111B	; ENABLE DMA TO TRANSFER

TABSIZ	EQU	$-DMAINI

H$TRK:	DEFW	0
H$SEC:	DEFB	1
RECNO:	DEFS	1

	DEFS	32		; SET 16 LEVELS STACK
STACKP:	DEFS	0

	.DEPHASE
	END

