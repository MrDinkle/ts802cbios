;
; TS-802H BOOT SECTOR FOR TANDON 5 1/4 WINCHESTER
; AND 5 1/4 FLOPPY DISK
;
;	EDIT BY TUYEN TRAN DATE 5/20/81
;
;	MODIFIED BY SUNG WOO LEE -- 9-18-81
;		TO BE ADOPTED FOR TS-802H
;
;	EXECUTES AT 4000H
;	IF INPUT A = 00H THEN READ IN CP/M FROM HARD DISK.
;		   = 01H THEN READ IN CP/M FROM FLOPPY DISK.
;	TURNS OFF PROM
;	READ IN CPM AND BIOS TO CCP ADDRESS
;	JMP TO BIOS=CCP+1600H
;
NSECTS	EQU	63	;REMAINING SECTORS ON FIRST TWO TRKS
PRMOFF	EQU	04H	;PORT TO TURN PROM OFF
INIR	EQU	0B2EDH	;Z-80 'INIR' INSTR.
OTIR	EQU	0B3EDH	;Z-80 'OTIR' INSTR.
;

	CSEG
	.PHASE	4000H
CCP	EQU	0D000H
BIOS	EQU	CCP+1600H
DIFF	EQU	$-4000H
;
;	STANDARD CP/M CONSTANTS
;
FALSE	EQU	0		;CONDITIONAL ASSEMBLY FLAGS
TRUE	EQU	NOT FALSE
;
CPMSIZ	EQU	1600H		;LENGTH OF CCP AND BDOS
NSECT	EQU	CPMSIZ/128	;NUMBER SECTORS TO WARM BOOT
BUFF	EQU	80H		;DEFAULT BUFFER
;
;	HARD DISK - CP/M INTERFACE
;
MAXHD	EQU	1		;NUMBER OF LOGICAL HARD DISKS
MAXSEC	EQU	32		;NUMBER OF SECTORS PER TRACK
MAXHED	EQU	4		;NUMBER OF HEADS
MAXCYL	EQU	153		;NUMBER OF CYLINDERS (ST-506)
PCMPCYL	EQU	128		;BEGIN WRITE PRECOMP. HERE
BLKSIZ	EQU	2048		; # BYTES PER BLOCK
CPMSPT	EQU	2*MAXSEC	; # CPM SECTORS PER TRACK
;
RPTERRS	EQU	TRUE		;REPORT ERROR STATUS TO OPERATOR
;
;   BIT FLAGS
;
F$RDOP	EQU	1		;READ OPERATION
F$RIP	EQU	2		;READ IN PROGRESS
F$WIP	EQU	4		;WRITE IN PROGRESS
F$INIT	EQU	080H		;CONTROLLER INITIALIZED
;
;   DRIVE COMMANDS
;
STEPRAT	EQU	7		;STEPPING RATE = 3.5 MS
;
;   DRIVE STATUS
;
WREADY	EQU	040H		;DRIVE IS READY
WFAULT	EQU	020H		;WRITE FAULT ON DRIVE
WCMPLT	EQU	010H		;SEEK COMPLETE
;
;   CONTROLLER COMMANDS
;
WREST	EQU	010H+STEPRAT	;BASIC RESTORE COMMAND + STEPPING RATE
WSEEK	EQU	060H+STEPRAT	;BASIC SEEK COMMAND + STEPPING RATE
WREAD	EQU	020H		;READ SECTOR
WWRITE	EQU	030H		;WRITE SECTOR
WFMT	EQU	050H		;FORMAT
;
;   CONTROLLER STATUS
;
WBUSY	EQU	080H		;CONTROLLER BUSY
WDRQ	EQU	008H		;DATA REQUEST
WERROR	EQU	001H		;ERROR STATUS PRESENT
;
WEBLK	EQU	080H		;BAD BLOCK DETECT
WECRCD	EQU	040H		;CRC ERROR - DATA FIELD
WECRCI	EQU	020H		;CRC ERROR - ID FIELD
WENOT	EQU	010H		;ID NOT FOUND
WEABORT	EQU	004H		;ABORTED COMMAND
WETR0	EQU	002H		;TRACK 000 ERROR
WEDAM	EQU	001H		;DATA ADDRESS MARK NOT FOUND
;
;   DRIVE/CONTROLLER I/O
;
CBASE	EQU	48H		;BASE ADR OF THE CONTROLLER
;
WDATA	EQU	CBASE		;DATA PORT (I/O)
WPCYL	EQU	CBASE+1		;WRITE PRECOMP CYLINDER NUMBER (O)
WERRS	EQU	CBASE+1		;ERROR DEFINITION (I)
WCOUNT	EQU	CBASE+2		;SECTOR COUNT (I/O)
WSECT	EQU	CBASE+3		;SECTOR NUMBER (I/O)
WLOCYL	EQU	CBASE+4		;CYLINDER NUMBER, LOW ORDER BITS(I-O)
WHICYL	EQU	CBASE+5		;CYLINDER NUMBER, HIGH ORDER BITS (I/O)
WSDH	EQU	CBASE+6		;SECTOR SIZE, DRIVE SELECT, HEAD SELECT. (I/O)
WCMD	EQU	CBASE+7		;COMMAND PORT (O)
WSTAT	EQU	CBASE+7		;STATUS PORT (I)
;
;   ASCII CHARACTERS
;
CR	EQU	0DH		;CARRIAGE RETURN
LF	EQU	0AH		;LINE FEED
CTLC	EQU	3		;CONTROL C TO ABORT
BELL	EQU	7		;DING

TIMER	EQU	7FFFH		;DELAY FOR RESET AND POWER ON.
;
LFLAG	EQU	0F000H		;BYTE #0 AND #1 FOR POWER UP ID.
				;BYTE #2 IF EVEN THEN BOOT FROM HARD DISK
				; IF ODD THEN BOOT FROM FLOPPY.
;
FLAG1	EQU	055H
FLAG2	EQU	0AAH		;RESET IDENTIFICATION.
;
;	FLOOPY DISK CONTROLLER EQUATES
;
DSELOP	EQU	18H		;DRIVE SELECT AND CONFIG. PORT ADDRESS
FDCCMD	EQU	14H		;COMMAND AND STATUS PORT.
FDCTRK	EQU	15H		;TRACK REG. PORT.
FDCSEC	EQU	16H		;SECTOR REG. PORT.
FDCDAT	EQU	17H		;DATA REG. PORT.
;
;	DMA PORT ADDRESS
;
DMA	EQU	10H		;DMA DEVICE PORT ADDRESS
;
;	FDC STATUS BITS
;
FDCBSY	EQU	0		;FDC BUSY BIT #0
FDCIND	EQU	1		;FDC INDEX HOLE DETECTED.
FDCTR0	EQU	2		;FDC TRACK 0 DETECTED.
FDCCRC	EQU	3		;FDC CRC ERROR ENCOUNTERED.
FDCSEE	EQU	4		;FDC SEEK ERROR ENCOUNTERED.
FDCHLD	EQU	5		;FDC HEAD LOAD ACK.
FDCPRT	EQU	6		;FDC DISK IS WRITE PROTECTED.
FDCRDY	EQU	7		;FDC DISK NOT READY BIT.
;
;	FDC STATUS BYTE MASKS
;
ERRMSK	EQU	11011100B	;MASK OFF INSIGNIFICANT BITS
SEEMSK	EQU	10011000B	;CHECK DRIVE NOT READY BIT.
DNRMSK	EQU	10000000B	;DRIVE NOT READY MASK
;
;	FDC COMMAND BYTES
;
RESCMD	EQU	00000111B	;HOME HEAD COMMAND.
SEECMD	EQU	00010011B	;SEEK TRACK COMMAND WITH NO VERIFY
RDSCMD	EQU	10000000B	;READ SECTOR
WTSCMD	EQU	10100000B	;WRITE SECTOR
FRCCMD	EQU	11010000B	;FORCE TERMINATION
;
;	FLOPPY DISK CONFIGURATION
;
FTPSIDE EQU	40		;NUMBER OF TRACK PER SIDE.
FSPT	EQU	18		;NUMBER OF SECTOR PER TRACK
FBPS	EQU	256		;NUMBER OF BYTES PER SECTOR.
DCONFIG EQU	00010010B	;SELECT DRIVE #0; SIDE #0; DDENSITY; MOTOR ON
;

	OUT	PRMOFF	;TURN PROM OFF
	LXI	SP,STACKP-DIFF	;INITIALIZE STACK POINTER.
	ORA	A		;CHECK 'BOOT' FROM WHERE
	JNZ	BFLOPPY-DIFF	;IF FROM FLOPPY THEN SKIP
;
;	ELSE LOAD CP/M FROM WINCHESTER.
;
	INR	A		;PRESET RECNO = 1
	STA	RECNO-DIFF
	LXI	H,CCP
;
LOOP:
	LDA	H$TRK-DIFF
	ANI	3
	OUT	WSDH
	XRA	A
	OUT	WHICYL
	OUT	WLOCYL
	LDA	H$SEC-DIFF
	OUT	WSECT
	MVI	A,1
	OUT	WCOUNT
;
	MVI	A,WREAD
	OUT	WCMD
GETIO:	IN	WSTAT
	ANI	WDRQ
	JZ	GETIO-DIFF
;
;	SET B = TRANSFER COUNT; C = INPUT DATA PORT ADDRESS
;
	LXI	B,WDATA
;
;	TRANSFER DATA FROM CONTROLLER TO MEMORY
;
	DW	INIR
;
WAIT:	IN	WSTAT
	ANI	WBUSY
	JNZ	WAIT-DIFF
;
	DCR	H		;SET HL = LAST DMA ADDRESS
	IN	WSTAT		;CHECK FOR ERROR
	ANI	WFAULT+WERROR
	JNZ	LOOP-DIFF	;IF ERROR THEN RETRY TRANS. DATA BLOCK
	INR	H		;ELSE HL = NEXT DMA ADDRESS.
;
	XCHG
;
	LXI	H,RECNO-DIFF
	MOV	A,M
	SUI	NSECTS
;
	JZ	BIOS
;
	INR	M
;
	DCX	H		;ADDRESS H$SEC.
	INR	M
	MOV	A,M
	CPI	32
	XCHG
	JNZ	LOOP-DIFF
	XCHG
	XRA	A
	MOV	M,A
;
	DCX	H
	DCX	H		;ADDRESS H$TRK.
	INR	M		;SET TRACK NO. TO NEXT
;
	XCHG
	JMP	LOOP-DIFF
;
BFLOPPY:
;	BOOT SYSTEM IS FAILED ON WINCHESTER HARD DISK
;	NOW TRY TO BOOT FROM FLOPPY.
;
;
	LXI	H,H$TRK-DIFF	;SYSTEM START AT TRACK #0
	XRA	A
	MOV	M,A
	INX	H
	INX	H
	MVI	M,2		;SECTOR #2
	INR	A
	INX	H
	MOV	M,A		;PRESET RECNO = 1
;
BFLOP0:
;	INITIALIZE DMA DEVICE
	MOV	B,6
	MVI	A,11000011B	;RESET DMA
BFLOP1:
	OUT	DMA
	DCR	B
	JNZ	BFLOP1-DIFF
;
BFLOP2:
	LXI	H,DMAINI-DIFF	;ADDRESS DMA INITIALIZE TABLE
	LXI	B,(TABSIZ*256)+DMA
;
	DW	OTIR
;
;	INITIALIZE FDC REGISTERS.
;
	LDA	H$TRK-DIFF
	OUT	FDCTRK		;SET FDC TRACK REGISTER
	LDA	H$SEC-DIFF
	MVI	B,00010010B	;SET B = DRIVE #0 AND SEL.SIDE #0
	CPI	FSPT+1		;NEED TO SELECT SIDE #1 ?
	JC	BFLOP4-DIFF	;IF NO THEN SKIP
	SUI	FSPT		;ELSE CONVERT SECTOR FOR SIDE #1 READ
	MVI	B,00010110B	;SET B = DRIVE #0 AND SEL.SIDE #1
BFLOP4:
	OUT	FDCSEC		;SET FDC SECTOR REGISTER
	MOV	A,B		;SET DISK CONFIG.
;
	OUT	DSELOP		;SELECT DRIVE, SIDE, DENSITY, TURN MOTOR ON
	MVI	A,RDSCMD	;READ SECTOR INTO MEMORY
	CALL	BFLOP6-DIFF
	JNZ	BFLOP0-DIFF	;IF ERROR THEN RETRY AGAIN.
;
	LXI	H,H$SEC-DIFF	;SET SECTOR ADDRESS TO NEXT.
	INR	M		;IF END OF SECTOR ADDRESS ON SIDE #1 OF CURRENT
	MOV	A,M		;TRACK THEN
	CPI	(FSPT*2)+1	;PRESET SECTOR ADDRESS TO 1; TRACK = TRACK+1
	JC	BFLOP9-DIFF	;ELSE SECTOR = SECTOR+1
	MVI	M,1
	DCX	H
	DCX	H
	INR	M
	MOV	A,M
	OUT	FDCDAT
;
;	SEEK TO NEW TRACK.
;
BFLO10:
	MVI	A,SEECMD
	CALL	BFLOP6-DIFF	;PERFORM SEEK OPERATION.
	JZ	BFLOP9-DIFF	;IF NO ERROR THEN SKIP.
	MVI	A,RESCMD	;ELSE RESET HEAD TO HOME IF ERROR.
	CALL	BFLOP6-DIFF
	JMP	BFLO10-DIFF	;RETRY SEEK OPERATION
;
BFLOP9:
	LXI	H,RECNO-DIFF	;CHECK READ COUNT
	MOV	A,M
	CPI	NSECTS		;IF FINISH
	MVI	A,1		;SET A = 1 AND GO TO BIOS COLD BOOT ENTRY
	JZ	BIOS
	INR	M		;ELSE READ COUNT = COUNT + 1
	LXI	H,TRANADD-DIFF+1	;SET DMA ADDRESS FOR NEXT READ
	INR	M
	JMP	BFLOP2-DIFF	;READ NEXT SECTOR IN.
;
BFLOP6:
	OUT	FDCCMD		;NOW READ SECTOR INTO MEMORY.
;
	MVI	A,15		;WE MUST DELAY ABOUT 60 US BEFOR READING
BFLOP7:
	DCR	A		;FDC STATUS.
	JNZ	BFLOP7-DIFF
;
BFLOP8:
	IN	FDCCMD		;READ FDC STATUS
	MOV	B,A
	ANI	10000000B	;CHECK FOR DRIVE NOT READY?
	RNZ
	MOV	A,B
	ANI	00000001B	;CHECK OPERATION COMPLETED
	JNZ	BFLOP8-DIFF	;WAIT UNTIL NOT BUSY
	MOV	A,B
	ANI	00011100B	;ANY ERROR: RNF,CRC,LOST DATA?
	RET
	
;
DMAINI:
	DB	11000011B	;RESET DMA CHANNEL
	DB	01101101B	;PORT 'A' ADDRESS FOLLOWS
	DB	FDCDAT		;FDC DATA PORT ADDRESS
	DW	0FFH		;READ TRANSFER COUNT
	DB	00111100B	;PORT 'A' IS FIXED
	DB	00010000B	;PORT 'B' MEMORY, INC.
	DB	11001101B	;PORT 'B' ADDRESS FOLLOWS
TRANADD:
	DW	CCP		;CP/M LOAD ADDRESS
	DB	10001010B	;READY ACTIVE HIGH, END OF BLOCK
	DB	11001111B	;LOAD REGISTERS
	DB	00000101B	;A -> B TRANSFER MODE.
	DB	11001111B	;LOAD REGISTERS
	DB	10000111B	;ENABLE DMA TO TRANSFER
TABSIZ	EQU	$-DMAINI

;
H$TRK:	DW	0
H$SEC:	DB	1
RECNO:	DS	1
;
	DS	32		;SET 16 LEVELS STACK
STACKP:	DS	0
;
;
	.DEPHASE
	END
