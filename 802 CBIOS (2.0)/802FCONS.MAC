
; TEST THE STATUS OF THE CONSOLE INPUT TO
; SEE IF A CHARACTER IS AVAILABLE.
;	EXIT:	A=0FFH	->	CHARACTER AVAILABLE
;		A=0	->	NO CHARACTER

CONST:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	0		; CONSOLE POSITION IN IOBYTE
	DEFW	TTYST		; CONSOLE PRINTER DEVICE
	DEFW	CRTST		; CONSOLE CRT DEVICE
	DEFW	BATST		; CONSOLE BATCH DEVICE
	DEFW	UC1ST		; USER DEFINED CONSOLE

; PRINTER STATUS ROUTINE FOR SERIAL
; INTERFACE WITH EPSON AND TI
; SIO #1 CHANNEL B

TTYST:	LD	A,00010000B	; RESET EXTRN/STATUS COMMAND
	OUT	(SIOBC1),A	; SEND OUT
	IN	A,(SIOBC1)	; READ SIO STATUS
	AND	00100100B	; CHECK TRANSMITT READY AND CTS
	CP	00100100B	; READY TO SEND?
	JR	Z,TTYS1		; YES, CODE FOR SET READY CONDITION
	XOR	A		; NO, SET NOT READY CONDITION
	RET

TTYS1:	XOR	A
	DEC	A		; A = FF
	RET

; CONSOLE STATUS ROUTINE FOR STANDARD
; CRT TERMINAL DEVICE, SIO #0 CHANNEL B.

CRTST:	IN	A,(SIOBC0)	; READ SIO STATUS
	BIT	RCVRDY,A	; CHARACTER AVAILABLE?
	LD	A,0
	RET	Z		; NO. A = 0

	DEC	A		; YES. A = FFH
	RET

; GRAB A CHARACTER FROM CONSOLE INPUT.
;	EXIT:	A=RETURNED CHARACTER

CONIN:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	0		; CONSOLE POSITION IN IOBYTE
	DEFW	TTYIN		; CONSOLE PRINTER DEVICE
	DEFW	CRTIN		; CONSOLE CRT DEVICE
	DEFW	BATIN		; CONSOLE BATCH DEVICE
	DEFW	UC1IN		; USER DEFINED CONSOLE

; JUST RETURN FOR NOW

TTYIN:	RET

; CONSOLE INPUT ROUTINE FOR
; STANDARD CRT TERMINAL DEVICE.
; SIO CHANNEL B.
; MOTOR AND LIGHT OFF IN HALF A SECOND
; WHEN THERE IS NO INPUT ON CONSOLE

CRTIN:	PUSH	BC
	LD	BC,8000H	; TIME-OUT FOR 1/2 SECOND
CRT20:	CALL	CRTST		; CHECK THE STATUS
	JR	NZ,CRT10	; SKIP 
	DEC	BC		; COUNT DOWN
	LD	A,B		; BC REACHED ZERO?
	OR	C
	JR	NZ,CRT20	; NO, LOOP BACK
	LD	A,0		; MOTOR AND LIGHT OFF
	OUT	(FDD),A		; SEND OUT
	LD	(MOTFLG),A	; MOTOR ON-OFF FLAG
	JR	CRT20		; LOOP UNTIL WE GET A CHARACTER
CRT10:	IN	A,(SIOBD0)	; FETCH THE CHARACTER
	AND	7FH		; STRIP OFF PARITY BIT 
	POP	BC
	RET

; OUTPUT A CHARACTER TO THE CONSOLE.
;	ENTRY:	C=CHARACTER TO BE OUTPUT

CONOUT:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	0		; CONSOLE POSITION IN IOBYTE
	DEFW	TTYOUT		; CONSOLE PRINTER DEVICE
	DEFW	CRTOUT		; CONSOLE CRT DEVICE
	DEFW	BATOUT		; CONSOLE BATCH DEVICE
	DEFW	UC1OUT		; USER DEFINED CONSOLE

; OUTPUT ROUTINE FOR SERIAL EPSON AND TI PRINTER
; SIO CHANNEL A.

TTYOUT:	CALL	TTYST		; CHECK STATUS OF PRINTER
	JR	Z,TTYOUT	; NOT READY, LOOP BACK
	LD	A,C		; GET CHARACTER IN REG. C
	AND	7FH		; STRIP OFF PARITY
	OUT	(SIOBD1),A	; SEND OUT
	RET

; OUTPUT ROUTINE FOR STANDARD
; CRT TERMINAL DEVICE, SIO
; CHANNEL B.

CRTOUT:	IN	A,(SIOBC0)	; CHECK STATUS OF TRANSMITTER
	BIT	TRXRDY,A	; TRANSMITTER BUFFER EMPTY?
	JR	Z,CRTOUT	; NOT YET.
	LD	A,C		; TRANSFER THE CHARACTER
	OUT	(SIOBD0),A	; OUTPUT THE CHARACTER TO SIO
	RET

; PAPER TAPE READER ROUTINE,
; MOSTLY UNUSED.

READER:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	2		; POSITION OF READER IN IOBYTE
	DEFW	TTYIN		; STANDARD TELETYPE DEVICE
	DEFW	PTRRDR		; HIGH SPEED PAPER TAPE READER
	DEFW	UR1RDR		; USER DEFINED READER #1
	DEFW	UR2RDR		; USER DEFINED READER #2
 	

; PAPER TAPE PUNCH ROUTINE,
; CURRENTLY DUMMIED OUT,
; PRETTY MUCH.

PUNCH:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	4		; PUNCH POSITION IN IOBYTE
	DEFW	TTYOUT		; TELETYPE OUTPUT
	DEFW	PTPPUN		; HIGH SPEED PUNCH DEVICE
	DEFW	UP1PUN		; USER DEFINED PUNCH #1
	DEFW	UP2PUN		; USER DEFINED PUNCH #2

; SEND A CHARACTER TO THE PARALLEL PORT.
; THIS PORT HAS A STANDARD CENTRONICS INTERFACE.
;	ENTRY:		C=CHARACTER TO BE OUTPUT

LIST:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	6		; LIST POSITION IN IOBYTE
	DEFW	TTYOUT		; OUTPUT TO TELETYPE DEVICE
	DEFW	CRTOUT		; OUTPUT TO CRT DEVICE
	DEFW	LPTLST		; LINE PRINTER DEVICE
	DEFW	UL1LST		; USER DEFINED PRINTING DEVICE

; THIS IS THE PRINTER OUTPUT ROUTINE
; FOR THE CENTRONICS PARALLEL DEVICE.

LPTLST:	CALL	LPTLSS		; CHECK STATUS
	JR	NZ,LPTLST	; LOOP UNTIL PRINTER READY
	LD	A,C		; LOAD THE CHARACTER
	OUT	(PIOBD),A	; INTO PIO CHANNEL B DATA REG.
	IN	A,(PIOAD)	; READ PRINTER CONTROL LINES
	RES	STROBE,A	; PULSE THE STROBE LINE
	OUT	(PIOAD),A	; TO INDICATE THAT DATA IS
	IN	A,(PIOAD)	; VALID IN CHANNEL B DATA PORT
	SET	STROBE,A	; RAISE STROBE LINE AGAIN
	OUT	(PIOAD),A	; COMPLETE STROBE SIGNAL
	RET


 
; HERE IS THE SERIAL PRINTER DRIVER, WHICH EXPECTS
; THE DEVICE ON THE OTHER END TO SUPPORT AN ETX/ACK
; PROTOCOL.  PORT USED IS SIO CHANNEL A.

UL1LST:	LD	A,(UL1SIZ)	; SIZE OF PRINTER BUFFER.
	LD	B,A		; FOR LATER.
	OR	A		; CLEAR CARRY
	RRA			; DIVIDE BY 2
	LD	E,A		; E <- UL1SIZ/2
	LD	HL,UL1CNT	; CURRENT BUFFER COUNT
	INC	(HL)		; BUMP IT.
 
UL1L10:	IN	A,(SIOBC1)	; READ STATUS 
	BIT	RCVRDY,A	; DO WE HAVE AN INPUT CHARACTER?
	JR	Z,UL1L20	; NO. SEND CHARACTER.
	IN	A,(SIOBD1)	; GRAB THE CHARACTER.
	AND	7FH		; STIP PARITY.
	CP	ASCACK		; IS IT ACK?
	JR	Z,UL1L60	; YES, PROCESS IT.
UL1L20:	LD	A,E		; GET UL1SIZ/2
	CP	(HL)		; IS BUFFER HALF-FULL?
	JR	NZ,UL1L40	; NO, NO NEED FOR ETX.
 
UL1L30:	IN	A,(SIOBC1)	; PREPARE FOR OUTPUT.
	BIT	TRXRDY,A	; CAN WE OUTPUT?
	JR	Z,UL1L30	; NOT YET.
	LD	A,ASCETX	; SEND OFF THE END-OF-
	OUT	(SIOBD1),A	; BLOCK CHARACTER.
	JR	UL1L50		; NOW OUTPUT THE CHARACTER
 
UL1L40:	LD	A,B		; ORIGINAL BUFFER SIZE
	CP	(HL)		; CHECK FOR FULL AND OVERFLOW.
	JR	Z,UL1L30	; FULL, SO SEND ANOTHER ETX
	JR	C,UL1L10	; OVERFLOW, WAIT FOR ACK.
 
UL1L50:	IN	A,(SIOBC1)	; THIS IS THE REAL OUTPUT
	BIT	TRXRDY,A	; ROUTINE AND EXIT
	JR	Z,UL1L50	; LOOP UNTIL TRANSMITTER READY.
	LD	A,C		; CHARACTER HAS BEEN IN REG. C
	AND	7FH		; ALL THIS TIME. STRIP PARITY.
	OUT	(SIOBD1),A	; SHIP IT!
	RET			; ONLY EXIT POINT.

UL1L60:	LD	A,(HL)		; DID WE EVER SEND AN ETX?
	SUB	E		; I.E., IS BUFFER > = HALF FULL?
	JR	Z,UL1L70	; YES.
	JR	C,UL1L50	; NO, SPURIOUS ACK.
 
UL1L70:	LD	(HL),A		; ADJUST THE BUFFER COUNT
	JR	UL1L50		; AND SEND THE CHARACTER.
 
; TEST THE STATUS OF THE PRINTER.
; THEN RETURN NOT READY FLAG.
;	EXIT:		A=0	->	PRINTER IS NOT READY
;			A=0FFH	->	PRINTER IS READY

LISTST:	CALL	IODISP		; DIRECT I/O TO PROPER DEVICE
	DEFB	6		; LIST POSITION IN IOBYTE
	DEFW	TTYST		; STATUS OF TELETYPE DEVICE
	DEFW	CRTST		; STATUS OF CRT DEVICE
	DEFW	LPTLSS		; STATUS OF LINE PRINTER DEVICE
	DEFW	UL1LSS		; STATUS OF USER DEFINED PRINTING DEVICE


; PARALLEL PRINTER STATUS CHECK.
; RETURNS PRINTER NOT READY BECAUSE:
; 1) IT IS OUT OF PAPER
; 2) IT IS BUSY
; 3) A FAULT OCCURED
 
LPTLSS:	IN	A,(PIOAD)	; READ PRINTER STATUS
;	AND	PIOMSK		; MASK OFF DON'T CARE BITS
;	XOR	PIOERR		; ERROR MASK, PRINTER READY?
;	JR	NZ,LPTS10	; NO.
;	DEC	A		; YES, RETURN FF IN A

	BIT	PRTBSY,A	; TEST PRINTER BUSY BIT
	LD	A,0
	RET	NZ
	CPL			; SET A = FFH AND ZERO FLAG = 1
	RET

LPTS10:	XOR	A		; INDICATE NOT READY
	RET			; BY RETURNING 0 IN A

; CHECK THE STATUS OF SERIAL PRINTER,
; CP/M UL1 DEVICE.

UL1LSS:	LD	HL,UL1CNT	; CURRENT BUFFER COUNT
	LD	A,(UL1SIZ)	; MAX COUNT.
	CP	(HL)		; ROOM FOR MORE CHARACTER?
	LD	A,0FFH		; ANTICIPATE AFFIRMATIVE.
	RET	NZ		; LISTER IS READY.
 
	IN	A,(SIOBC1)	; OTHERWISE SEE IF WE HAVE
	BIT	RCVRDY,A	; AN INPUT CHARACTER READY.
	LD	A,0FFH		; ANTICIPATE AFFIRMATIVE AGAIN.
	RET	NZ		; WE ARE ASSUMING CHAR. IS AN ACK.
 
	XOR	A		; WE MUST NOT BE READY YET
	RET

; FOLLOWING IS JUST A GENERAL-PURPOSE
; DUMMY ROUTINE, USED FOR UNDEFINED BIOS
; HOOKS, AND UNUSED IOBYTE DRIVERS.

UNUSED:	RET			; JUST RETURN FOR NOW

; PAPER TAPE READER, PUNCH AND CONSOLE
; DUMMY ROUTINES.  USER MAY FILL OUT LATER.

BATIN:
UC1IN:

BATOUT:
UC1OUT:

PTRRDR:
UR1RDR:
UR2RDR:

PTPPUN:
UP1PUN:
UP2PUN:

	LD	A,1AH		; RETURN EOF.
	RET

; DUMMY CONSOLE STATUS ROUTINES 

BATST:
UC1ST:
	XOR	A		; ALWAYS BUSY
	RET

; END OF 802FCONS.MAC
