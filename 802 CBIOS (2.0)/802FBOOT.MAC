
;  **************** BIOS JUMP TABLE *****************

BIOS:	JP	BOOT
	JP	WBOOT
	JP	CONST
	JP	CONIN
	JP	CONOUT
	JP	LIST
	JP	PUNCH
	JP	READER
	JP	HOME
	JP	SELDSK
	JP	SETTRK
	JP	SETSEC
	JP	SETDMA
	JP	READ
	JP	WRITE
	JP	LISTST
	JP	SECTRN
	JP	UNUSED
	JP	UNUSED
	JP	UNUSED
	JP	UNUSED
	JP	UNUSED

; COPYRIGHT STATEMENT

	DEFM	'(C) 1981, TELEVIDEO SYSTEMS INC.'

; VERSION AND REVISION NO.

VER:	DEFB	2
REV:	DEFB	0


; COME HERE FROM COLDSTART LOADER

BOOT:	OUT	(PROMSW),A	; SHUT PROM OFF (DEBUG)
	LD	SP,CPMBUF+80H	; INITIALIZE STACK POINTER
	LD	HL,SIGNON	; PRINT SIGNON MESSAGE
	CALL	PRTMSG

	XOR	A		; MAKE LOGGED DRIVE 0
	LD	(DRIVE),A	; SAVE IT FOR LATER
	LD	(LOGDRV),A
	LD	A,00010101B	; INITIALIZE IOBYTE
	LD	(IOBYTE),A
	CALL	HDWINI		; INITIALIZE SYSTEM HARDWARE
	JR	GOCPM		; DO MORE INITIALIZATION

; WE JUMP TO HERE WHENEVER THE USER WANTS
; TO RESTART THE SYSTEM, I. E., REREAD CP/M
; WITHOUT MODIFYING THE BIOS

WBOOT:	LD	SP,CPMBUF+80H	; INITIALIZE STACK POINTER
	LD	A,NOTRYS	; NUMBER OF RETRIES FOR BOOT
	LD	(BOOTRY),A	; STORE IT.	
	XOR	A		; PREVENT BUFFER FLUSH
	LD	(HSTACT),A	; CONDITION ON WRITE ERROR

WBOO10:	CALL	RESDSK		; RESET DISK SUBSYSTEM

; INITIALIZE THE TRANSFER PARAMETERS

	LD	C,0		; SELECT DRIVE ZERO
	CALL	SELDSK		; FROM WHICH TO BOOT
	LD	HL,CPMBAS	; INITIAL DMA ADDRESS
	LD	DE,0002H	; D=INITIAL TRACK, E=INITIAL SECTOR
	LD	B,BDOSLN/RECLEN	; LENGTH OF TRANSFER, IN SECTORS

WBOO20:	PUSH	BC		; SAVE REGISTERS
	PUSH	HL
	PUSH	DE
	LD	C,L		; MOVE TRANSFER ADDRESS TO BC
	LD	B,H
	CALL	SETDMA		; SET DATA TRANSFER ADDRESS
	LD	C,D
	CALL	SETTRK		; SET TRACK REGISTER TO D
	LD	C,E
	CALL	SETSEC		; SET SECTOR REGISTER TO E	
	CALL	READ		; READ A SECTOR
	JR	Z,WBOO30	; READ SUCCESSFUL
	POP	DE		; ELSE RETORE REGISTERS
	POP	HL
	POP	BC
	LD	HL,BOOTRY	; CHECK ON RETRY SITUATION
	DEC	(HL)
	JR	NZ,WBOO10	; MORE TRIES LEFT.
	LD	HL,BOOMSG	; PRINT ERROR MESSAGE
	CALL	PRTMSG
	JR	$		; AND DIE.

WBOO30:	POP	DE		; ADJUST TRACK AND SECTOR
	INC	E		; BUMP SECTOR NO.
	LD	A,E
	CP	SPT0		; END OF CURRENT TRACK?
	JR	C,WBOO40	; NO.
	LD	E,0		; CYCLE THE SECTOR NO.
	INC	D		; AND INCREMENT THE TRACK

WBOO40:	POP	HL		; RECALL THE TRANSFER ADDRESS
	LD	BC,RECLEN	; ADD IN A SECTOR
	ADD	HL,BC
	POP	BC		; RESTORE SECTOR COUNT
	DJNZ	WBOO20		; GO READ NEXT SECTOR.	

; CODE SHARED BY BOTH WARM AND COLD STARTS

GOCPM:	LD	BC,CPMBUF	; INITIALIZE DMA ADDRESS
	CALL	SETDMA
	LD	A,JP		; INITIALIZE LOW MEMORY
	LD	(0000H),A
	LD	HL,BIOS+3	; WARM BOOT JUMP ENTRY
	LD	(0001H),HL	; STORE IT
	LD	(0005H),A
	LD	HL,BDOS		; BDOS ENTRY POINT
	LD	(0006H),HL	; STORE IT
	XOR	A		; CLEAR SERIAL PRINTER BUFFER COUNT
	LD	(UL1CNT),A	; ON COLD OR WARM START
	LD	(UNACNT),A	; UNALLOCATED SECTOR COUNT
	LD	(HSTACT),A	; BUFFER INACTIVE
	LD	A,(LOGDRV)	; PASS LOGGED DRIVE NO.
	LD	C,A		; IN REG-C
	JP	CPMBAS		; TO CCP

; END OF 802FBOOT.MAC
